
plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'jacoco'
    id 'com.gradleup.shadow' version '8.3.1'
    id 'se.thinkcode.cucumber-runner' version '0.0.11'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}



dependencies {
    testImplementation libs.junit.jupiter
    testImplementation(platform("io.cucumber:cucumber-bom:7.19.0"))
    testImplementation("io.cucumber:cucumber-java")
    testImplementation("io.cucumber:cucumber-junit-platform-engine")
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation 'org.assertj:assertj-swing:3.17.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'edu.wofford.App'
}

jar {
    manifest {
        attributes 'Main-Class': 'edu.wofford.app.App'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        html {
            enabled true
        }
        xml {
            enabled true
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                        '**/edu/wofford/Main*',
                        '**/edu/wofford/ConsoleMain*',
                        '**/edu/wofford/GuiMain*']
                    )
        }))
    }
}

javadoc {
    options.with {
        links 'https://docs.oracle.com/javase/17/docs/api/'
    }
    exclude 'edu/wofford/Main*'
    exclude 'edu/wofford/ConsoleMain*'
    exclude 'edu/wofford/GuiMain*'
}

cucumber {
    glue = 'gradle.cucumber'
    plugin = ['pretty', 'html:app/build/reports/cucumber/report.html']
    main = 'io.cucumber.core.cli.Main'
}



